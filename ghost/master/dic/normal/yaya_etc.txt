//******************************************************************************
// YAYA　ゴーストテンプレート
// 種々のイベント辞書
//******************************************************************************

//******************************************************************************
//リンクが飛んできた
//******************************************************************************

OnXUkagakaLinkOpen
{
	_t = ''
	_r0 = SHIORI3FW.EscapeAllTags(reference[0])
	
	if isGhostLaunchFromLink == 1 {
		_t += '\1\s[10]\0'
		_t += '\![call,ghost,"'
		_t += _r0
		_t += '"]'
		_t += '\e'
	}
	elseif ISVAR('isGhostLaunchFromLink') == 0 {
		_t += 'おっ、ワイ宛てにリンクが飛んできたやで。\n'
		_t += '指定された名前のゴーストを起動するようにするか？\n[150]'
		_t += '\![*]\__q[OnXUkagakaLinkOpen_Setting,"'
		_t += _r0
		_t += '"]起動する　\__q\n'
		_t += '\![*]\__q[OnXUkagakaLinkOpen_NoSetting,fromlink]放っといて\__q'
		_t += '\e'
	}
	
	_t
}

OnXUkagakaLinkOpen_Setting
{
	isGhostLaunchFromLink = 1
	_t =  '\1\s[10]\0'
	
	if STRLEN(reference[0])>0 {
		_t += '了解やで～。\nほな、さっきのを起動するやで。\w9\w9'
		_t += '\![call,ghost,"'
		_t += reference[0]
		_t += '"]'
		_t += '\e'
		_t
	}
	else {
		_t += '了解やで～。\nほな、メニューに戻るで。\x\c'
		_t
		--
		OnMenu
	}
}

OnXUkagakaLinkOpen_NoSetting
{
	isGhostLaunchFromLink = 0
	_t =  '\1\s[10]\0'
	
	if STRLEN(reference[0])>0 {
		_t += '了解やで～。\e'
		_t
	}
	else {
		_t += '了解やで～。\nほな、メニューに戻るで。\x\c'
		_t
		--
		OnMenu
	}
}


//******************************************************************************
//シェル切り替え
//******************************************************************************

//------------------------------------------------------------------------------
//シェルチェンジ開始
//------------------------------------------------------------------------------

OnShellChanging
{
	'\1\s[10]\0'
	--
	'\s[0]覗くなよ。\w9\n\s[0]や、\w5覗けるもんなら覗いてもええけど…\w5…\w5。'
	'\s[0]覗くなよ。\w9\n…\w5…\w5\s[0]あは～ん。'
	'\s[0]ミラクルでマジカルなお色直しタイムやで。'
	'\s[0]変身中の攻撃は勘弁やで。'
	--
	'\w9\w9\e'
}

//------------------------------------------------------------------------------
//シェルチェンジ完了
//------------------------------------------------------------------------------
OnShellChanged
{
	'\1\s[10]\0'
	--
	'\s[0]どやろか。'
	'\s[0]お色直し完了やで。'
	'\s[0]お色直し完了やで。\w9\n…\w5…\w5\s[0]うふ～ん。'
	--
	'\e'
}


//******************************************************************************
//インストール関連
//******************************************************************************

//------------------------------------------------------------------------------
//インストール中のメッセージ
//------------------------------------------------------------------------------
OnInstallBegin
{
	'\1\s[10]\0\s[0]おう、インストールするで。\w9\e'
}

//------------------------------------------------------------------------------
//インストール完了メッセージ
//------------------------------------------------------------------------------
OnInstallComplete
{
	_ref0 = SPLIT(reference[0],' with',2)[0]//hogehoge with hemohemo

	'\1\s[10]\0\s[0]'
	--
	if _ref0 == 'shell' {
		'シェル'
	}
	elseif _ref0 == 'ghost' {
		'ゴースト'
	}
	elseif _ref0 == 'balloon' {
		'バルーン'
	}
	elseif _ref0 == 'plugin' {
		'プラグイン'
	}
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[1])
	"「%(_escaped_name)」、インストール完了や。\n"
	--
	if basewarenameex == 'SSP' {
		_id = ''
		if _ref0 == 'shell' {
			_id = 'shell'
		}
		elseif _ref0 == 'ghost' {
			_id = 'ghost'
		}
		elseif _ref0 == 'balloon' {
			_id = 'balloon'
		}

		if _id != '' {
			'切り替えるか？'
			--
			AYATEMPLATE.MenuItem("切り替える","OnAYATEMPLATE.InstallChange",_id,200)
			--
			AYATEMPLATE.MenuItem("切り替えない","Menu_CANCEL")
		}
	}
}

OnAYATEMPLATE.InstallChange
{
	"\![change,%(AYATEMPLATE.EscapeText(reference[0])),lastinstalled]"
}

//------------------------------------------------------------------------------
//インストール失敗メッセージ
//------------------------------------------------------------------------------
OnInstallFailure
{
	_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
	"\1\s[10]\0\s[0]インストール失敗やで‥\w5‥\w5\n\n理由は「%(_escaped_message)」。\e"
}

//------------------------------------------------------------------------------
//インストール拒否（他のゴーストのためのアーカイブだった）
//------------------------------------------------------------------------------
OnInstallRefuse
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"\1\s[10]\0\s[0]これは「%(_escaped_name)」用のアーカイブやで。\w9処理中断。\e"
}


//******************************************************************************
//消滅関連
//******************************************************************************

//------------------------------------------------------------------------------
//消滅指示
//------------------------------------------------------------------------------
OnVanishSelecting
{
	'\1\s[10]\0\s[0]お役御免か？\e'
}

//------------------------------------------------------------------------------
//消滅開始
//------------------------------------------------------------------------------
OnVanishSelected
{
	'\1\s[10]\0\s[0]今まで使うてくれて、\w5おおきにやで。\w9\w9\e'
}

//------------------------------------------------------------------------------
//消滅中止
//------------------------------------------------------------------------------
OnVanishCancel
{
	'\1\s[10]\0\s[0]まだまだ、\w5よろしゅうな。'
}


//******************************************************************************
//メールチェック
//******************************************************************************

//------------------------------------------------------------------------------
//メールチェック開始
//------------------------------------------------------------------------------
OnBIFFBegin
{
	'\1\s[10]\0メールチェックを開始するで。\e'
}

//------------------------------------------------------------------------------
//メールチェック成功
//------------------------------------------------------------------------------
OnBIFFComplete
{
	'\1\s[10]\0'
	--
	if TOINT(reference[0]) == 0{
		//---- メールは無かった
		'\s[0]新着は無かったで。'
	}
	else{
		//---- メールが届いていた
		"\s[0]%(reference[0])通、\w5新着があったで。"
		"\s[0]%(reference[0])通、\w5新着があったで。\w9\n\s[0]忘れんうちに見ときや。"
	}
	--
	'\e'
}

//------------------------------------------------------------------------------
//メールチェック失敗
//------------------------------------------------------------------------------
OnBIFFFailure
{
	'\1\s[10]\0\s[0]メールチェック失敗やで。\w9\n'
	--
	if reference[0] == 'timeout'{
		//---- タイムアウト
		'時間切れっぽいな。'
	}
	elseif reference[0] == 'kick'{
		//---- 蹴られた
		'蹴られたで。'
	}
	elseif reference[0] == 'defect'{
		//---- 設定誤り
		'設定は間違っとらんか？'
	}
	else{
		//---- 原因不明
		'原因はようわからん。'
	}
	--
	'\e'
}


//******************************************************************************
//ネットワーク更新
//******************************************************************************

//------------------------------------------------------------------------------
//ネットワーク更新開始
//------------------------------------------------------------------------------
OnUpdateBegin
{
	'\1\s[10]\0\s[0]更新があるかどうか、調べるで。\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新準備完了
//------------------------------------------------------------------------------
OnUpdateReady
{
	"\1\s[10]\0\s[0]%(TOINT(reference[0])+1)個、降ってくるやで。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新終了
//------------------------------------------------------------------------------
OnUpdateComplete
{
	'\1\s[10]\0'
	--
	if reference[0] == 'none'{
		//---- 更新すべきファイルが無かった
		'\s[0]更新は特に無かったで。'
	}
	else{
		//---- 実際に更新が完了した
		'\s[0]更新完了や。'
	}
	--
	'\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新失敗
//------------------------------------------------------------------------------
OnUpdateFailure
{
	'\1\s[10]\0\s[0]更新失敗やで。\w9\n'
	--
	if reference[0] == 'timeout'{
		//---- タイムアウトした
		'原因は時間切れ。'
	}
	elseif reference[0] == 'md5 miss'{
		//---- MD5値が一致しなかった
		'原因はMD5値不一致。\w9\n配布元に報告して貰えると助かるで。\w9\n\s[0]右クリックから飛べるから、\w5よかったら頼むな。'
	}
	else{
		//---- 上記以外の理由で失敗した
		_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
		"原因は「%(_escaped_message)」。"
	}
	--
	'\e'
}


//******************************************************************************
//ヘッドラインセンス
//******************************************************************************

//------------------------------------------------------------------------------
//ヘッドラインセンス開始
//------------------------------------------------------------------------------
OnHeadlinesenseBegin
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"\1\s[10]\0\s[0]%(_escaped_name)ヘッドライン取得中やで。\e"
}

//------------------------------------------------------------------------------
//ヘッドライン読み上げ
//------------------------------------------------------------------------------
OnHeadlinesense.OnFind
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	_escaped_body = SHIORI3FW.EscapeAllTags(reference[3])
	//---- はじめのセリフ
	if reference[2] == "First" || reference[2] == "First and Last"{
		//---- 最初のページ
		"\0\b2\s[0]%(_escaped_name)のヘッドラインやで。"
	}
	elseif reference[2] == 'Next'{
		//---- 真ん中あたりのページ
		"\0\b2\s[0]%(_escaped_name)ヘッドライン、続きや。"
	}
	elseif reference[2] == 'Last'{
		//---- 最後のページ
		"\0\b2\s[0]%(_escaped_name)ヘッドライン、これが最後のページやね。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%(_escaped_body)\n\n"
	--
	//---- 選択肢
	if reference[2] == "First" || reference[2] == "Next"{
		//---- 次のページがある場合
		AYATEMPLATE.MenuItem("次のページ","")
		--
		AYATEMPLATE.MenuItem('サイトへ',reference[1])
		--
		AYATEMPLATE.MenuItem("やめる","Menu_HEADLINECANCEL",150)
	}
	else{
		//---- 次のページがない場合
		AYATEMPLATE.MenuItem('サイトへ',reference[1])
		--
		AYATEMPLATE.MenuItem("やめる","Menu_HEADLINECANCEL",150)
	}
}

//------------------------------------------------------------------------------
//ヘッドライン更新が無かった
//------------------------------------------------------------------------------
OnHeadlinesenseComplete
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"\1\s[10]\0\s[0]%(_escaped_name)、\w5更新は無いみたいや。\e"
}

//------------------------------------------------------------------------------
//ヘッドラインセンス失敗
//------------------------------------------------------------------------------
OnHeadlinesenseFailure
{
	'\1\s[10]\0\s[0]ヘッドラインセンスに失敗したで。\w9\n\n理由は「'
	--
	if reference[0] == "can't download"{
		'ダウンロード失敗'
	}
	elseif reference[0] == "can't analyze"{
		'ヘッドライン解析失敗'
	}
	else{
		SHIORI3FW.EscapeAllTags(reference[0])
	}
	--
	'」。\e'
}


//******************************************************************************
//ファイル作成関連
//******************************************************************************

//------------------------------------------------------------------------------
//updates2.dau作成開始
//------------------------------------------------------------------------------
OnUpdatedataCreating
{
	'\1\s[10]\0\s[0]アップデートデータを作成するで。\e'
}

//------------------------------------------------------------------------------
//updates2.dau作成完了
//------------------------------------------------------------------------------
OnUpdatedataCreated
{
	'\1\s[10]\0\s[0]アップデートデータ、作成完了やで。\e'
}

//------------------------------------------------------------------------------
//NAR作成開始
//------------------------------------------------------------------------------
OnNarCreating
{
	'\1\s[10]\0\s[0]narを作るで。\e'
}

//------------------------------------------------------------------------------
//NAR作成完了
//------------------------------------------------------------------------------
OnNarCreated
{
	"\1\s[10]\0\s[0]「%(SHIORI3FW.EscapeAllTags(reference[0]))」のnar、\w5「%(SHIORI3FW.EscapeAllTags(reference[1]))」に作成完了やで。\e"
}

//******************************************************************************
//時計合わせ
//みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏
//******************************************************************************

//------------------------------------------------------------------------------
//時計合わせ開始
//------------------------------------------------------------------------------
OnSNTPBegin
{
	"\1\s[10]\0\s[0]時計合わせ開始。\w9%(SHIORI3FW.EscapeAllTags(reference[0]))に接続するで。\e"
}

//------------------------------------------------------------------------------
//時刻取得成功、比較開始
//------------------------------------------------------------------------------

SNTPCompare_StrForm
{
	_txt = SPLIT(_argv[0],',')
	STRFORM('$04d/$02d/$02d $02d:$02d:$02d',TOINT(_txt[0]),TOINT(_txt[1]),TOINT(_txt[2]),TOINT(_txt[3]),TOINT(_txt[4]),TOINT(_txt[5]))
}

OnSNTPCompare
{
	"\1\s[10]\0\s[0]\_q/
	修正前 %(SNTPCompare_StrForm(reference[2]))\n/
	修正後 %(SNTPCompare_StrForm(reference[1]))\n/
	\_q\n"
	--
	if reference[3] == 0{
		'ぴったしやね。\e'
	}
	else{
		//\6は補正実行タグ。消しちゃだめ
		"\6\0\n%(SHIORI3FW.EscapeAllTags(reference[3]))秒のずれを修正したで。"
	}
}

//------------------------------------------------------------------------------
//時計合わせ失敗
//------------------------------------------------------------------------------
OnSNTPFailure
{
	'\1\s[10]\0\s[0]時計合わせ失敗。\w9なんでかはわからん。\w9\e'
}


//******************************************************************************
//そのほか
//******************************************************************************

//------------------------------------------------------------------------------
//ディスプレイ解像度変更
//------------------------------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference[0]
	displaywidth  = reference[1]
	displayheight = reference[2]
	SHIORI3FW.RegisterTempVar('displaybpp', 'displaywidth', 'displayheight')
}

//------------------------------------------------------------------------------
//URLドロップ
//CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます
//------------------------------------------------------------------------------
OnURLDropping
{
	"\1\s[10]\0\s[0]%(SHIORI3FW.EscapeAllTags(reference[0]))からデータを取得しとるで。\e"
}

