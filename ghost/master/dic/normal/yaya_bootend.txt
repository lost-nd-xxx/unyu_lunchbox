//******************************************************************************
// YAYA　ゴーストテンプレート
// 起動/終了トーク辞書
//******************************************************************************

//******************************************************************************
//初回起動 OnFirstBoot
//******************************************************************************

OnFirstBoot
{
	//---- 変数の初期化
	username = 'ユーザ'
	
	'\1\s[10]\0\s[0]や。\w9うにゅう言います、\w5よろしゅう頼むな。\w9\n[150]ワイと仲良うやる前に、\w5音量調整するとええかも。\w9\n知らんけど。'
	--
	'\e'
}


//******************************************************************************
//通常の起動 OnBoot
//******************************************************************************

OnBoot
{
	//---- まず双方のサーフィスを表示
	'\1\s[10]\0\s[0]'
	--
	//---- 起動時間を確認してあいさつを変化
	_timeslot = GetTimeSlot

  // 必要に応じて音声をプリロード（判定はSoundsPreload関数内で行う）
  SoundsPreload

	case _timeslot{
		when '朝'{
			'おはようさん。'
		}
		when '昼'{
			'腹、\w5減っとらんか？'
			'こんちゃ～す、\w5やで。'
		}
		when '日中'{
			'こんちゃ～す、\w5やで。'
		}
		when '夜'{
			'おこんばんやす。'
		}
		when '深夜'{
			'おばんやす～。'
		}
		others{
			//それ以外の時間の場合
			'また会うたね。'
		}
	}//caseの終わり
	
	--
	'\e'
}


//------------------------------------------------------------------------------
//朝/昼/夜などをおおざっぱに調べる関数
//------------------------------------------------------------------------------
GetTimeSlot
{
	case hour{
		when 22,23,0,1,2,3 {'深夜'}
		when 4,5,6,7,8,9,10,11 {'朝'}
		when 12,13 {'昼'}
		when 14,15,16,17 {'日中'}
		when 18,19,20,21 {'夜'}
	}//caseの終わり
}


//******************************************************************************
//終了 OnClose
//******************************************************************************

OnClose
{
	'\1\s[10]\0\s[0]'
	--
	//この部分には\eつけないでね！
	_timeslot = GetTimeSlot
	if _timeslot == '朝' {
		//---- 朝に終了
		'今日も一日、\w5気張っていきや～。'
		'行ってら～。'
	}
	elseif _timeslot == "昼" || _timeslot == "日中" {
		//---- 昼/日中に終了
		'ほなな～。'
		'ワイは昼寝しとくわ。'
	}
	elseif _timeslot == '夜' || _timeslot == "深夜" {
		'おやすみやで～。'
		'ええ夢、\w5見ぃや～。'
	}
	else{
		'ほな、\w5おおきに。'
	}
	--
	'\w9\w9\-'
}


//******************************************************************************
//最小化からの復帰 OnWindowStateRestore
//******************************************************************************

OnWindowStateRestore
{
	'\1\s[10]\0\s[0]'
	--
	'おう、\w5出番やね。'
	'おう、\w5出番やね。'
	'おう、\w5出番やね。'
	'もう出てええん？'
	--
	'\e'
}

OnSystemLoad.DefaultValue
{
	//リセットする
	MenuItemState = ''
	InputProcessing = ''
	UnyuLunchBox_Path = GETSETTING( 'coreinfo.path' )

	//規定値を入れる
	if ISVAR('ButtonSound_Enable') == 0 { ButtonSound_Enable = 1 }
	if ISVAR('ButtonSound_Kind') == 0 { ButtonSound_Kind = 'random' }
	if ISVAR('ButtonSound_FilePath') == 0 { ButtonSound_FilePath = UnyuLunchBox_Path + 'sound\1up_1.wav' }
	if ISVAR('LunchFood_Kind') == 0 { LunchFood = 'soundonly' }
	if ISVAR('LunchFood_LanchPath') == 0 { LunchFood_LanchPath = _path + 'okazu.txt' }
}

SoundsPreload
{
	if ButtonSound_Kind == 'random' && ButtonSound_Enable == 1 {
		_sound = 効果音_すべて
		_script = ''
		foreach _sound; _i {
			_script += '\![sound,load,"' + _i + '"]'
		}
		_script
	}
	elseif ButtonSound_Enable == 1 {
		_sound = SHIORI3FW.EscapeAllTags( ButtonSound_FilePath )
		'\![sound,load,"' + _sound + '"]'
	}
}
